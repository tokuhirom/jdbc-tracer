// releng: ./gradlew clean && ./gradlew uploadArchives -Prelease
// build: ./gradlew build
buildscript {
    repositories {
        mavenCentral()
        //Needed only for SNAPSHOT versions
        //maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.9'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: "info.solidsoft.pitest"

    pitest {
        pitestVersion = "1.1.0" //not needed when a default PIT version should be used
        threads = 4
        outputFormats = ['XML', 'HTML']
    }

    group 'me.geso.jdbctracer'
    version '0.1.1'

    repositories {
        mavenCentral()
        jcenter()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from "${buildDir}/javadoc"
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    compileJava {
        options.compilerArgs << '-XDignore.symbol.file' << '-Xlint:all'
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    task jacoco(type: JacocoReport, dependsOn: 'test') {
        sourceSets sourceSets.main
        executionData = files(tasks.jacocoTestReport.executionData)
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = true
        }
    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
